plugins {
    id 'fabric-loom' version "${loom_version}" apply false
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'fabric-loom'
    apply plugin: 'io.github.goooler.shadow'

    version = "${mod_version}"
    group = "${mod_group_id}.fabric"

    ext {
        minecraft_version_earliest = "${project.minecraft_version_list}".split(",")[0]
        minecraft_version_range = ">=${project.minecraft_version_earliest}-"
    }

    base {
        archivesName = "${mod_id}-${loader_name}-${minecraft_version}"
    }

    def isJava17Ports = "${project.minecraft_version}".replace(".", "").toInteger() < 1205

    java {
        withSourcesJar()
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21

        if (isJava17Ports) {
            sourceCompatibility = JavaVersion.VERSION_17
            targetCompatibility = JavaVersion.VERSION_17
        }
    }

    dependencies {

        minecraft "com.mojang:minecraft:${minecraft_version}"

        mappings loom.officialMojangMappings() // reserved for fabric loom

        modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
        modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"

        modCompileOnly "org.quiltmc:quilt-loader:${quilt_loader_version}"

        implementation project(':common')

        shadow project(':common')

        implementation 'com.google.code.findbugs:jsr305:3.0.2'

        annotationProcessor 'com.google.auto.service:auto-service:1.1.1'
        compileOnly 'com.google.auto.service:auto-service:1.1.1'
    }

    processResources {

        duplicatesStrategy = DuplicatesStrategy.INCLUDE

        filesMatching('fabric.mod.json') {
            expand project.properties
        }

        from project(':common').sourceSets.main.resources
    }

    shadowJar {
        configurations = [project.configurations.shadow]
        mergeServiceFiles()
        dependencies {
            exclude dependency('com.google.code.findbugs:jsr305')
        }
        relocate 'dev.huskuraft.effortless.vanilla', 'dev.huskuraft.effortless.fabric'
        relocate 'com.electronwill.nightconfig', 'dev.huskuraft.effortless.api.nightconfig'
    }

    remapJar {
        dependsOn shadowJar
        inputFile = shadowJar.archiveFile
    }

    loom {
        accessWidenerPath = file("src/main/resources/effortless.accesswidener")
        mixin {
            add(sourceSets.main, 'effortless.fabric.refmap.json')
        }
    }

    repositories {
        maven {
            name = 'Quilt'
            url = 'https://maven.quiltmc.org/repository/release'
        }
    }

}
